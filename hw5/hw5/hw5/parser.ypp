%{
    #include "includes.h"
    
    //#define YYERROR_VERBOSE 1
    //#define YYDEBUG 1
%}
/* Type tokens */
%token VOID // special case: can be a return type, but not a variable declaration
%token INT
%token BYTE
%token B
%token BOOL
/* Binary Arithmetic Operations */
%left PLUS MINUS
%left MULT DIV
/* Logical Operations */
%left AND
%left OR
%left NOT
/* Values */
%token TRUE
%token FALSE
%token NUM
%token STRING
/* Brackets and Parenthesis */
%left LPAREN RPAREN
%left LBRACE RBRACE
%left LBRACK RBRACK
/* Control Flow */
%token IF
%nonassoc ELSE
%token WHILE
%token BREAK
%token RETURN
/* Relational Operators */
/* keep them separated inorder to
 give different precedence to every op */
%nonassoc RELOP
%left BINOP
/* Assigment */
%right ASSIGN
/* ID */
%token ID
/* Separators */
%token SC
%token COMMA

%%

Program      : mInit Funcs { rule_Program__end();}
     
;

mInit         : { rule_init(); }
Funcs        : FuncDecl Funcs  
             | /*epsilon*/
;

FuncDecl     : FuncHeader LBRACE Statements RBRACE
                { rule_FuncBody($3); closeScope(); }
;

FuncHeader   : RetType ID LPAREN Formals RPAREN  
                { rule_FuncHeader((Type*)$1, (Variable*)$2, (FormList*)$4); }


RetType      : Type
                { $$=$1; }
             | VOID
                { $$=$1; }
;
Formals      : FormalsList
                { $$ = $1;}
             | /*epsilon*/
                { $$ = new FormList(); /*empty list*/}
;
FormalsList  : FormalDecl
                { $$ = new FormList(); ((FormList*)$$)->add((Variable*)$1); }
             | FormalDecl COMMA FormalsList
                { $$ = rule_FormalsList((Variable*)$1, (FormList*)$3); }
;

FormalDecl   : Type ID
                { $$ = rule_FormalDecl__Type_ID((Type*)$1, (Variable*)$2); }
             | Type ID LBRACK NUM RBRACK
                { $$ = rule_FormalDecl__Type_ID_NUM((Type*)$1, (Variable*)$2, (Expression*)$4); }
             | Type ID LBRACK NUM B RBRACK
                { $$ = rule_FormalDecl__Type_ID_NUMB((Type*)$1, (Variable*)$2, (Expression*)$4); }
;

Statements   : Statement
                {$$ = $1;}
             | Statements M Statement
                { rule_Statements($1, $2); $$ = $3; }
;


Statement    : LBRACE mOpenScope Statements M RBRACE mCloseScope
                { rule_Statements($3, $4); $$ = $3;}
             | Type ID SC
                { rule_Statement__Type_ID_SC((Type*)$1, (Variable*)$2); }
             | Type ID LBRACK NUM RBRACK SC
                { rule_Statement__Type_ID_NUM_SC((Type*)$1, (Variable*)$2, (Expression*)$4); }
             | Type ID LBRACK NUM B RBRACK SC
                { rule_Statement__Type_ID_NUMB_SC((Type*)$1, (Variable*)$2, (Expression*)$4); }
             | Type ID ASSIGN Exp SC
                { rule_Statement__Type_ID_ASSIGN_Exp_SC((Type*)$1, (Variable*)$2, (Expression*)$4); }
             | ID ASSIGN Exp SC
                { rule_Statement__ID_ASSIGN_Exp_SC((Variable*)$1, (Expression*)$3); }
             | ID LBRACK Exp RBRACK ASSIGN Exp SC
                { rule_Statement__ID_Exp_ASSIGN_Exp_SC((Variable*)$1, (Expression*)$3, (Expression*)$6); }
             | Call SC
             | RETURN SC
                { rule_Statement__RETURN_SC(); }
             | RETURN Exp SC
                { rule_Statement__RETURN_Exp_SC((Expression*)$2); }
             | IF LPAREN Exp RPAREN mOpenScope M Statement mCloseScope N
                /* S->if(B) { S1 }*/
                { $$ = rule_Statement__IF_Statement((Expression*)$3, $6, $7, $9); }
             | IF LPAREN Exp RPAREN mOpenScope M Statement mCloseScope N ELSE mOpenScope M Statement mCloseScope
                /* S->if(B) { S1 } else { S2 }*/
                { $$ = rule_Statement__IF_ELSE_Statement((Expression*)$3, $6, $7, $9, $12, $13); }
             | WHILE M LPAREN Exp RPAREN mOWhileScope M Statement mCWhileScope
                /* S->while(B) { S1 }*/
                { $$ = rule_Statement__WHILE_Statement((Expression*)$2, $4, $7, $8); }
             | BREAK SC { rule_Statement__BREAK_SC(); }
;

mOpenScope  : { openScope(); }  
mCloseScope : { closeScope(); }  
mOWhileScope: { openWhileScope(); }
mCWhileScope: { closeWhileScope(); }


Call         : ID LPAREN ExpList RPAREN
                { $$ = rule_Call__ID_ExpList((Variable*)$1, (ExprList*)$3); }
             | ID LPAREN RPAREN
                { $$ = rule_Call__ID((Variable*)$1); }
;

ExpList      : Exp
                { $$ = new ExprList(); ((ExprList*)$$)->v.push_back((Expression*)$1); }
             | Exp COMMA ExpList
                { $$ = $3; ((ExprList*)$$)->v.push_back((Expression*)$1); }
;
Type         : INT
                { $$=$1; }
             | BYTE
                { $$=$1; }
             | BOOL
                { $$=$1; }
;
Exp          : LPAREN Exp RPAREN
                { $$=$2; }
             | ID LBRACK Exp RBRACK
                { $$ = rule_Exp__ID_Exp((Variable*)$1, (Expression*)$3); }
             | Exp BINOP Exp
                { $$ = rule_Exp__Exp_BINOP_Exp((Expression*)$1, $2->token, (Expression*)$3); }
             | ID
                { $$ = rule_Exp__ID((Variable*)$1); }
             | Call
                /* store result (if numerical) in register */
                { $$ = $1; }
             | NUM
                { $$ = rule_Exp__NUM((Expression*)$1); }
             | NUM B
                {
                    if (atoi(((Expression*)$1)->value.c_str()) > 255) {
                        output::errorByteTooLarge(yylineno, ((Expression*)$1)->value);
                        exit(0);
                    }
                    ((Expression*)$1)->type = M_BYTE;
                    $$ = rule_Exp__NUM((Expression*)$1);
                }
             | STRING
                { $$ = rule_Exp__STRING((Expression*)$1); }
             | TRUE
                { $$ = rule_Exp__TRUE(); }
             | FALSE
                { $$ = rule_Exp__FALSE(); }
             | NOT Exp
                { $$ = rule_Exp__NOT_Exp((Expression*)$2); }
             | Exp AND M Exp
                { $$ = rule_Exp__Exp_AND_Exp((Expression*)$1, $3, (Expression*)$4); }
             | Exp OR M Exp
                { $$ = rule_Exp__Exp_OR_Exp((Expression*)$1, $3, (Expression*)$4); }
             | Exp RELOP Exp
                { $$ = rule_Exp__Exp_RELOP_Exp((Expression*)$1, $2->token, (Expression*)$3); }
;

M           : { $$ = marker__M(); }
N           : { $$ = marker__N(); }

%%

void yyerror(const char*) { output::errorSyn(yylineno); }

int main() {
    //yydebug = 1;
    return yyparse();
}

